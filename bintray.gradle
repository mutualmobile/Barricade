apply plugin: 'maven-publish'

afterEvaluate {
  publishing {
    publications {
      mavenJava(MavenPublication) {
        groupId = GROUP
        artifactId = 'barricade'
        version = VERSION_NAME
        pom {
          packaging 'aar'
          groupId GROUP
          artifactId bintrayName

          // Add your description here
          name =  libraryName
          description POM_DESCRIPTION
          url = POM_SITE_URL

          // Set your license
          licenses {
            license {
              name = POM_LICENCE_NAME
              url = POM_LICENCE_URL
            }
          }
          developers {
            developer {
              id = POM_DEVELOPER_ID
              name = POM_DEVELOPER_NAME
              email = POM_DEVELOPER_EMAIL
            }
          }
          scm {
            connection = POM_GIT_URL
            developerConnection = POM_GIT_URL
            url = POM_SITE_URL
          }
        }

      }
    }
    repositories {
      maven {
        url "$buildDir/releases"
      }
    }
  }
}

if (project.hasProperty("android")) {
  // Android libraries
  task sourcesJar(type: Jar) {
    classifier("sources")
    from android.sourceSets.main.java.srcDirs
  }

  task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  }
} else {
  // Java libraries
  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier("sources")
    from sourceSets.main.allSource
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier("javadoc")
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

